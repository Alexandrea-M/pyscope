#! /bin/csh -f
#
#  findasteroids:
#  C shell script
#
#  Assume wcs has already been run, i.e. WCS header fields exist in fits file
#  Run findstars for 3 images, then run movobj to search output for moving
#  objects
#
#  Last update DJA 010110
#

set starlimit = 2.0
set astlimit = 2.0
set maxlimit = 600
capy:
   if ("$1" == "-s") then
      set starlimit = $2
   else if ("$1" == "-a") then
      set astlimit = $2
   else if ("$1" == "-f") then
      set maxlimit = $2
   else
      goto bara
   endif
   shift; shift
   goto capy
bara:

if ($#argv != 3) then
   cat << +

Usage:  $0:t  [options]  file1 file2 file3
Options, which take default values (arcsec) if omitted, are
   -s <angle>  (default 2.0)  --  objects closer than this are assumed to be
                                  the same star
   -a <angle>  (default 2.0)  --  max. allowed error for asteroids in image 2
                                  assuming constant motion from image 1 to 3
   -f <angle>  (default 600)  --  fastest motion (angular difference between
                                  images 1 and 3) that will be detected
Files must be 3 FITS files with WCS headers

+
   exit
endif

set flag = false
foreach file ($1 $2 $3)
   if (-e $file:r.sta) set flag = true
end
if (-e movobj.out) set flag = true
if ($flag == true) then
   echo "The following files will be deleted/overwritten:"
   foreach file ($1 $2 $3)
      echo "   $file:r.sta"
   end
   echo "   movobj.out"
   echo -n "Is it ok to proceed (Y/N)?  "
   set zebra = ($<)
   if ($zebra !~ Y* && $zebra !~ y*) exit
   foreach file ($1 $2 $3)
      rm -f $file:r.sta movobj.out
   end      
endif

foreach file ($1 $2 $3)
   echo findstars -h $file ...
   findstars -h $file > $file:r.sta
end

movobj << +
$1:r.sta
$2:r.sta
$3:r.sta
$starlimit
$astlimit
$maxlimit
+
